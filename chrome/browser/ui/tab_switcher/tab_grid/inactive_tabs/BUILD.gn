# Copyright 2023 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/ios/swift_source_set.gni")

source_set("inactive_tabs") {
  configs += [ "//build/config/compiler:enable_arc" ]
  sources = [
    "inactive_tabs_coordinator.h",
    "inactive_tabs_coordinator.mm",
    "inactive_tabs_mediator.h",
    "inactive_tabs_mediator.mm",
  ]
  deps = [
    ":inactive_tabs_ui",
    "//base",
    "//components/favicon/ios",
    "//ios/chrome/browser/main:public",
    "//ios/chrome/browser/shared/coordinator/chrome_coordinator",
    "//ios/chrome/browser/snapshots",
    "//ios/chrome/browser/tabs/inactive_tabs:features",
    "//ios/chrome/browser/ui/tab_switcher",
    "//ios/chrome/browser/ui/tab_switcher:tab_utils",
    "//ios/chrome/browser/ui/tab_switcher/tab_grid/grid:grid_ui",
    "//ios/chrome/browser/url",
    "//ios/chrome/browser/web_state_list",
    "//ios/web/public",
    "//ui/gfx",
  ]
}

source_set("inactive_tabs_ui") {
  configs += [ "//build/config/compiler:enable_arc" ]
  sources = [
    "inactive_tabs_view_controller.h",
    "inactive_tabs_view_controller.mm",
  ]
  deps = [
    "//ios/chrome/app/strings",
    "//ios/chrome/browser/tabs/inactive_tabs:features",
    "//ios/chrome/browser/ui/tab_switcher/tab_grid:tab_grid_ui_constants",
    "//ios/chrome/browser/ui/tab_switcher/tab_grid/grid:grid_ui",
    "//ui/base",
  ]
}

source_set("inactive_tabs_button") {
  configs += [ "//build/config/compiler:enable_arc" ]
  sources = [
    "inactive_tabs_button_mediator.h",
    "inactive_tabs_button_mediator.mm",
  ]
  deps = [
    ":inactive_tabs_button_ui",
    "//base",
    "//ios/chrome/browser/main:public",
    "//ios/chrome/browser/tabs/inactive_tabs:features",
    "//ios/chrome/browser/web_state_list",
  ]
}

source_set("inactive_tabs_button_ui") {
  configs += [ "//build/config/compiler:enable_arc" ]
  sources = [ "inactive_tabs_count_consumer.h" ]
  deps = [ ":inactive_tabs_button_ui_swift" ]
}

swift_source_set("inactive_tabs_button_ui_swift") {
  bridge_header = "inactive_tabs_button_ui_swift_bridge.h"
  sources = [
    "inactive_tabs_button.swift",
    "inactive_tabs_button_header.swift",
  ]
  deps = [
    "//ios/chrome/app/strings",
    "//ios/chrome/common/ui/colors:swift",
    "//ui/base",
  ]
  frameworks = [ "UIKit.framework" ]
}
